void arm_thick_set(){
  for(int i=0; i<ARM1_NODE; i++){
    float h = ARM1_LENGTH/2;
    float t = i * 1.0/(ARM1_NODE-1);
    thickh1[i] = h * cos(pow(t,1.3)*(QUARTER_PI*0.6));
    thickw1[i] = thickh1[i]; 
  }
  for(int i=0; i<ARM2_NODE; i++){
    float r = thickh1[ARM1_NODE-1];
    float t = i * 1.0/(ARM2_NODE-1);
    thickh2[i] = r * cos(pow(t,0.8)*QUARTER_PI);
    thickw2[i] = r * cos(pow(t,0.8)*QUARTER_PI*0.7);   
  }
}

void hand_pt_set(){
  float l = HAND_LENGTH/HAND_NODE;
  for(int i=0;i<HAND_NODE;i++){
    float t = i * 1.0/(HAND_NODE-1);
    float h = thickh2[ARM2_NODE-1] * cos(t*QUARTER_PI*1.3);
    float w = thickw2[ARM2_NODE-1] * cos(t*QUARTER_PI*1.3); 
    float x = i * l; 
    for(int j=0;j<ARM_EDGE;j++){
      float y = h * sin(j * TWO_PI/ARM_EDGE);
      float z = w * cos(j * TWO_PI/ARM_EDGE);      
      hand_pt[i][j] = new PVector(x,y,z);
    }
  }
  float fng_y = 0;
  float fng_x = HAND_LENGTH*0.7;
  float fng1_z = thickw2[ARM2_NODE-1]/1.2;
  float fng2_z = thickw2[ARM2_NODE-1]/2;
  float fng3_z = 0;   
  float fng4_z = -thickw2[ARM2_NODE-1]/2; 
  PVector fng1_pos = new PVector(fng_x/2, fng_y, fng1_z);
  PVector fng2_pos = new PVector(fng_x*0.9, fng_y, fng2_z); 
  PVector fng3_pos = new PVector(fng_x, fng_y, fng3_z);    
  PVector fng4_pos = new PVector(fng_x*0.9, fng_y, fng4_z);      
  float fng1_angle = -PI/8;
  float fng2_angle = -PI/8 * 3;
  float fng3_angle = -PI/8 * 4;
  float fng4_angle = -PI/8 * 5;    
  fng1_pt = fing_make(fng1_pos, fng1_angle);
  fng2_pt = fing_make(fng2_pos, fng2_angle);  
  fng3_pt = fing_make(fng3_pos, fng3_angle);    
  fng4_pt = fing_make(fng4_pos, fng4_angle);        
}

PVector[][] fing_make(PVector pos, float phi){
  float l = (SCALE/9)/FING_NODE;
  PVector[][] pt = new PVector[FING_NODE][FING_EDGE]; 
  for(int i=0;i<FING_NODE;i++){
    float t = i * 1.0/(FING_NODE-1);
    float r = l * cos(t*QUARTER_PI);
    PVector bone = new PVector(0,1).rotate(phi).mult(i*l);
    for(int j=0;j<FING_EDGE;j++){
      float plot = j * TWO_PI/FING_EDGE;
      float x = r * sin(plot) * cos(phi);
      float z = r * sin(plot) * sin(phi);
      float y = r * cos(plot);
      pt[i][j] = new PVector(x+pos.x+bone.x, y+pos.y, z+pos.z+bone.y);
    }
  }
  return pt;
}



void arm_make(float value1, float value2, float segrot1, float segrot2){
  float l1 = ARM1_LENGTH;
  float ud1 = map(value1, -1, 2, PI/4, -PI/2);
  PVector ep1 = new PVector(l1*cos(ud1), -l1*sin(ud1));  
  PVector cp1 = new PVector(0,0);
  if(ep1.y<0){
    cp1.y = cos(atan2(ep1.x,ep1.y))*l1/2;
  }else{
    cp1.x = cos(atan2(ep1.x,ep1.y))*l1/2;
  }  
  rotateX(segrot1);
  arm_pt_set(ARM1_NODE, ep1, cp1, thickh1, thickw1);
  translate(ep1.x, ep1.y, ep1.z);
  rotateX(segrot2);
  float l2 = ARM2_LENGTH;  
  float ud2 = map(value2, 0, 1, 0, -2.5*PI/4);
  PVector ep2 = new PVector(l2*cos(armrot-ud2), l2*sin(armrot-ud2)); 
  PVector cp2 = new PVector(cos(armrot)*l2/2,abs(sin(armrot)*l2/2));
  arm_pt_set(ARM2_NODE, ep2, cp2, thickh2, thickw2);
  translate(ep2.x, ep2.y, ep2.z);
  rotateZ(armrot);
  hand_make();
}

void arm_pt_set(int node, PVector ep, PVector cp, float[] thickh, float[] thickw){
  int NODE_NUM = node;
  float rot = 0;
  PVector zpos = new PVector(0,0);
  PVector[][] pt = new PVector[NODE_NUM][ARM_EDGE];
  //bone cal
  for(int i=0; i<NODE_NUM; i++){ 
    float t = i * 1.0/(NODE_NUM-1);
    float x = (2 * cp.x * t * (1-t)) + (ep.x * pow(t,2));
    float y = (2 * cp.y * t * (1-t)) + (ep.y * pow(t,2)); 
    rot = -1 * atan2(-1*(y-zpos.y),x-zpos.x);
    if(i==0){rot=armrot;}
    zpos = new PVector(x,y);
    //set points
    for(int j=0; j<ARM_EDGE; j++){
      float z = thickw[i] * cos(j * 2 * PI/ARM_EDGE);
      PVector v = new PVector(0, thickh[i] * sin(j * 2 * PI/ARM_EDGE)).rotate(rot);
      pt[i][j] = new PVector(v.x + x, v.y + y, z); 
    }
  }
  armrot = rot;
  make_mesh(NODE_NUM, ARM_EDGE, pt); 
}

void hand_make(){
  make_mesh(HAND_NODE, ARM_EDGE, hand_pt);
  make_mesh(FING_NODE, FING_EDGE, fng1_pt);
  make_mesh(FING_NODE, FING_EDGE, fng2_pt);  
  make_mesh(FING_NODE, FING_EDGE, fng3_pt);
  make_mesh(FING_NODE, FING_EDGE, fng4_pt);     
}
