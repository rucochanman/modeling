//to main
int HEAD_NODE = 18;
int HEAD_EDGE = 14;
float HEAD_SIZE = 150;
PVector[][] head_pt = new PVector[HEAD_EDGE][HEAD_NODE];

int EYE_NODE = 6;
int EYE_EDGE = 14;
PVector eye_pos = new PVector(0,0,0);
float eye_angle = 0;
PVector[][] eye_pt = new PVector[EYE_NODE][EYE_EDGE];

void setup(){
  size(500, 500, P3D);
  noLoop();
  noStroke();
  frameRate(3);
  head_pt_set();
  eye_pt_set();

 
}

void draw(){
  
  background(200);
  //lights();
  pushMatrix();
  translate(width/2,height/2);
  scale(1.3);
  rotateX(-3*PI/8);
  rotateY(0*PI/8);
  //rotateY(frameCount * PI/8);
  stroke(0);
  //strokeWeight(5);
  face_mesh();
  fill(0,0,255);
  pushMatrix();
  translate(eye_pos.x,eye_pos.y,eye_pos.z);
  rotateX(PI/9);
  rotateY(PI/7);
  make_mesh(EYE_NODE, EYE_EDGE, eye_pt);
  popMatrix();
  pushMatrix();
  translate(-eye_pos.x,eye_pos.y,eye_pos.z);
  rotateX(PI/9);
  rotateY(-PI/7);  
 // rotateY(-2*PI/8);
  make_mesh(EYE_NODE, EYE_EDGE, eye_pt);
  popMatrix();

  popMatrix();
  
  
}  
  
void head_pt_set(){
  //variables
  float w = 1.58;
  float amp1 = 0.15;
  float ampf2 = 0.1;
  float ampb2 = 0.1;
  float ffreq1 = 3.2;
  float ffreq2 = 2.6;
  float bfreq1 = 0.4;
  float bfreq2 = 1.15;
  float phase1 = 1.7;
  float fphase2 = -0.5;
  float bphase2 = -2.5;
  float c = 0.44;
  for(int i=0; i<HEAD_EDGE; i++){    
    int n = 0;
    if(i>HEAD_EDGE/2){n = 2*(HEAD_EDGE/2-i);}
    float p = pow((i+n)*1.0/(HEAD_EDGE/2),2);
    float scale = p*w/1.5 + (1-p)*w;
    if(i==0){scale = 1.48;}
    for(int j=0; j<HEAD_NODE; j++){  
      float t = j*1.0/(HEAD_NODE-1);
      float x1 = 3 * HEAD_NODE/2.5 * t * pow((1-t),2);
      float x2 = 3 * HEAD_NODE/2.5 * pow(t,2) * (1-t);
      float x3 = (HEAD_NODE-1) * pow(t,3);
      float step = x1 + x2 + x3; 
      //params
      float ampf = (1-t)*amp1 + t*ampf2;
      float ampb = (1-t)*amp1 + t*ampb2;      
      float ffreq = (1-t)*ffreq1 + t*ffreq2;
      float bfreq = (1-t)*bfreq1 + t*bfreq2;
      float fphase = (((HEAD_NODE-1)-step)*phase1 + step*fphase2)/(HEAD_NODE-1);          
      float bphase = (((HEAD_NODE-1)-step)*phase1 + step*bphase2)/(HEAD_NODE-1);                
      //synthesis
      float front = ampf * sin(step*ffreq*PI/(HEAD_NODE-1)+fphase) + c;
      float back = ampb * sin(step*bfreq*PI/(HEAD_NODE-1)+bphase) + c;
      float synth = (1-p)*front + p*back;      
      //set points
      float thita = PI - step * PI/(HEAD_NODE-1);
      float phi = i * PI/(HEAD_EDGE/2);
      float z = HEAD_SIZE * synth * sin(thita) * cos(phi) * scale;
      float x = HEAD_SIZE * synth * sin(thita) * sin(phi) * scale;
      float y = HEAD_SIZE * synth * cos(thita);
      head_pt[i][j] = new PVector(x,y,z);
      point(x,y,z);
    }
  }
  eye_pos.x = HEAD_SIZE * 0.15;
  eye_pos.y = HEAD_SIZE * -0.08;
  eye_pos.z = HEAD_SIZE * 0.265;
  
  
  println(head_pt[1][7].z);
}


void eye_pt_set(){
  float thick, r, x, y;
  float z = 0;
  for(int i=0;i<EYE_NODE;i++){
    r = (EYE_NODE-1-i) * HEAD_SIZE/5.7/EYE_NODE;
    thick = pow((EYE_NODE-i)*1.0/EYE_NODE,17)*26;
    z += thick;
    for(int j=0;j<EYE_EDGE;j++){
      x = r * cos(j*TWO_PI/(EYE_EDGE-1));
      y = r * sin(j*TWO_PI/(EYE_EDGE-1));
      eye_pt[i][j] = new PVector(x,y,z);
    }
  }
}




void face_mesh(){
  beginShape(TRIANGLE_STRIP);
  for(int i=0; i<HEAD_EDGE; i++){
    for(int j=0; j<HEAD_NODE; j++){
      if(i==HEAD_EDGE-1){
        vertex(head_pt[i][j].x,head_pt[i][j].y,head_pt[i][j].z);
        vertex(head_pt[0][j].x,head_pt[0][j].y,head_pt[0][j].z);
      }else{        
        vertex(head_pt[i][j].x,head_pt[i][j].y,head_pt[i][j].z);
        vertex(head_pt[i+1][j].x,head_pt[i+1][j].y,head_pt[i+1][j].z);
      }
    }
  }
  endShape(CLOSE);
}


void make_mesh(int NODE_NUM, int EDGE_NUM, PVector[][] pt){
  for(int i=0; i<EDGE_NUM; i++){
    beginShape(TRIANGLE_STRIP);
    for(int j=0; j<NODE_NUM; j++){
      vertex(pt[j][i].x,pt[j][i].y,pt[j][i].z);
      if(i==EDGE_NUM-1){
        vertex(pt[j][0].x,pt[j][0].y,pt[j][0].z);
      }else{
        vertex(pt[j][i+1].x,pt[j][i+1].y,pt[j][i+1].z);
      }
    }
    endShape();
  }
  beginShape();
  for(int i=0; i<EDGE_NUM; i++){
    vertex(pt[NODE_NUM-1][i].x,pt[NODE_NUM-1][i].y,pt[NODE_NUM-1][i].z);
  }
  endShape();
}








 
      
